dnl ***************************
dnl *** Version information ***
dnl ***************************
m4_define([mousepad_version_major], [0])
m4_define([mousepad_version_minor], [5])
m4_define([mousepad_version_micro], [8])
m4_define([mousepad_version_nano], []) dnl leave this empty to have no nano version
m4_define([mousepad_version_build], [@REVISION@])
m4_define([mousepad_version_tag], [git])
m4_define([mousepad_version], [mousepad_version_major().mousepad_version_minor().mousepad_version_micro()ifelse(mousepad_version_nano(), [], [], [.mousepad_version_nano()])ifelse(mousepad_version_tag(), [git], [mousepad_version_tag()-mousepad_version_build()], [mousepad_version_tag()])])

dnl *******************************************
dnl *** Debugging support for GIT snapshots ***
dnl *******************************************
m4_define([mousepad_debug_default], [ifelse(mousepad_version_tag(), [git], [yes], [minimum])])

dnl ***************************
dnl *** Initialize autoconf ***
dnl ***************************
AC_COPYRIGHT([Copyright (C) 2005-2021
        The Mousepad developers. All rights reserved.])
AC_INIT([Mousepad], [mousepad_version], [https://gitlab.xfce.org/apps/mousepad], [mousepad])
AC_PREREQ([2.50])
AC_CONFIG_MACRO_DIRS([m4])
AC_CANONICAL_TARGET()
AC_REVISION([])

dnl ***************************
dnl *** Initialize automake ***
dnl ***************************
AM_INIT_AUTOMAKE([1.8 no-dist-gzip dist-bzip2 tar-ustar])
AC_CONFIG_HEADERS([config.h])
AM_MAINTAINER_MODE()
m4_ifdef([AM_SILENT_RULES], [AM_SILENT_RULES([yes])])

dnl *******************************
dnl *** Check for UNIX variants ***
dnl *******************************
AC_USE_SYSTEM_EXTENSIONS
AC_SEARCH_LIBS([strerror],[cposix])

dnl ********************************
dnl *** Check for basic programs ***
dnl ********************************
AC_PROG_CC()
AM_PROG_CC_C_O()
LT_PATH_LD([])
AC_PROG_INSTALL()
AC_PROG_LN_S
AC_PROG_SED
IT_PROG_INTLTOOL([0.35.0])

dnl **************************
dnl *** Initialize libtool ***
dnl **************************
LT_PREREQ([2.2.6])
LT_INIT([disable-static])

dnl **************************************
dnl *** Substitute version information ***
dnl **************************************
MOUSEPAD_VERSION_MAJOR=mousepad_version_major()
MOUSEPAD_VERSION_MINOR=mousepad_version_minor()
MOUSEPAD_VERSION_MICRO=mousepad_version_micro()
MOUSEPAD_VERSION_NANO=mousepad_version_nano()
AC_SUBST([MOUSEPAD_VERSION_MAJOR])
AC_SUBST([MOUSEPAD_VERSION_MINOR])
AC_SUBST([MOUSEPAD_VERSION_MICRO])
AC_SUBST([MOUSEPAD_VERSION_NANO])

dnl ************************************
dnl *** Check for standard libraries ***
dnl ************************************
AC_SEARCH_LIBS([round], [m], [], [
  AC_MSG_ERROR([unable to find the round() function in math library])
])
AC_CHECK_FUNCS(realpath)

dnl **********************************
dnl *** Check for standard headers ***
dnl **********************************
AC_CHECK_HEADERS([errno.h libintl.h locale.h math.h stdio.h stdlib.h string.h])

dnl ******************************
dnl *** Check for i18n support ***
dnl ******************************
XDT_I18N([@LINGUAS@])

dnl ***********************************
dnl *** Check for required packages ***
dnl ***********************************
AC_DEFINE(GLIB_VERSION_MAX_ALLOWED, GLIB_VERSION_2_52, Warn when using post 2_52 APIs)
AC_DEFINE(GLIB_VERSION_MIN_REQUIRED, GLIB_VERSION_2_52, Don't warn about post 2_52 deprecations)
XDT_CHECK_PACKAGE([GLIB], [glib-2.0], [2.52.0])
XDT_CHECK_PACKAGE([GMODULE], [gmodule-2.0], [2.52.0])
XDT_CHECK_PACKAGE([GTK], [gtk+-3.0], [3.22.0])

dnl *******************************
dnl *** GtkSourceView 4 support ***
dnl *******************************
AC_ARG_ENABLE([gtksourceview4],
              [AS_HELP_STRING([--enable-gtksourceview4],
                              [Build against GtkSourceView 4 instead of GtkSourceView 3])],
              [enable_gtksourceview4=$enableval],
              [enable_gtksourceview4=auto])
AS_IF([test "x$enable_gtksourceview4" = "xauto"],
      [PKG_CHECK_MODULES([GTKSOURCEVIEW], [gtksourceview-4],
                         [enable_gtksourceview4=yes], [enable_gtksourceview4=no])])
AS_IF([test "x$enable_gtksourceview4" = "xyes"],
      [PKG_CHECK_MODULES([GTKSOURCEVIEW], [gtksourceview-4 >= 4.0.0])])
AS_IF([test "x$enable_gtksourceview4" = "xno"],
      [PKG_CHECK_MODULES([GTKSOURCEVIEW], [gtksourceview-3.0 >= 3.24.0])])

dnl ************************************
dnl *** Enable support for GSettings ***
dnl ************************************
GLIB_GSETTINGS

AC_ARG_ENABLE([keyfile-settings],
              [AS_HELP_STRING([--enable-keyfile-settings],
                              [Use the GSettings keyfile backend rather than the default])],
              [enable_keyfile_settings="yes"],
              [enable_keyfile_settings="default"])

AM_CONDITIONAL([WITH_KEYFILE_SETTINGS],
               [test "x$enable_keyfile_settings" = "xyes"])

dnl *************************
dnl *** Check for plugins ***
dnl *************************
MOUSEPAD_PLUGIN_GSPELL()
MOUSEPAD_PLUGIN_TEST()

dnl ***********************************
dnl *** Check for debugging support ***
dnl ***********************************
XDT_FEATURE_DEBUG([mousepad_debug_default])

dnl **************************************
dnl *** Check for linker optimizations ***
dnl **************************************
XDT_FEATURE_LINKER_OPTS()

dnl *********************************
dnl *** Substitute platform flags ***
dnl *********************************
AC_MSG_CHECKING([PLATFORM_CPPFLAGS])
AC_MSG_RESULT([$PLATFORM_CPPFLAGS])
AC_SUBST([PLATFORM_CPPFLAGS])
AC_MSG_CHECKING([PLATFORM_CFLAGS])
AC_MSG_RESULT([$PLATFORM_CFLAGS])
AC_SUBST([PLATFORM_CFLAGS])
AC_MSG_CHECKING([PLATFORM_LDFLAGS])
AC_MSG_RESULT([$PLATFORM_LDFLAGS])
AC_SUBST([PLATFORM_LDFLAGS])

AC_CONFIG_FILES([
Makefile
mousepad/Makefile
plugins/Makefile
plugins/gspell-plugin/Makefile
plugins/test-plugin/Makefile
po/Makefile.in
icons/Makefile
icons/16x16/Makefile
icons/48x48/Makefile
icons/128x128/Makefile
icons/scalable/Makefile
])
AC_OUTPUT

dnl ***************************
dnl *** Print configuration ***
dnl ***************************
echo
echo "Build Configuration:"
echo
echo "* Debug Support:              $enable_debug"
echo "* Use keyfile backend:        $enable_keyfile_settings"
echo "* Build with GtkSourceView 4: $enable_gtksourceview4"
echo
echo "Plugins:"
echo
echo "* gspell:                     $ac_mousepad_plugin_gspell"
echo "* test:                       $ac_mousepad_plugin_test"
echo
